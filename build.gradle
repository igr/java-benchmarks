plugins {
	id 'java'
}

version = '0.1.0'
group = 'com.oblac'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.jodd:jodd-core:5.1.5'
	implementation 'org.openjdk.jmh:jmh-core:1.37'
	annotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
}

compileJava {
	sourceCompatibility = '11'
	targetCompatibility = '11'
	options.debug = true
	options.fork = true
	options.encoding = 'UTF-8'
	options.compilerArgs << "-Xlint:-options"
	options.incremental = true
}

task jmh(type:JavaExec, dependsOn: 'build') {
	group 'Application'
	description 'Execute benchmarks.'

	main 'org.openjdk.jmh.Main'
	classpath = sourceSets.main.runtimeClasspath
}

tasks.addRule("Pattern: <ClassName>Benchmark : Run benchmark.") { String className ->
	if (className.endsWith("Benchmark")) {
		println "\nRUNNING BENCHMARK: " + className + "\n"

		task "$className"(type: JavaExec, dependsOn: 'build') {
			main 'org.openjdk.jmh.Main'
			args =  ['.' + className + '.*']
			classpath = sourceSets.main.runtimeClasspath
		}
	}
}
