description = '''
	JMH benchmarks.
'''

version = '0.1.0'
group = 'rs.zuehlke'

repositories {
	mavenCentral()
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'eclipse'

dependencies {
	compile 'org.openjdk.jmh:jmh-core:1.19'
	compile 'org.openjdk.jmh:jmh-generator-annprocess:1.19'
}

compileJava {
	sourceCompatibility = '1.8'
	targetCompatibility = '1.8'
	options.debug = true
	options.fork = true
	options.encoding = 'UTF-8'
	options.compilerArgs << "-Xlint:-options"
	options.incremental = true
}

tasks.withType(Javadoc) {
	options.addStringOption('Xdoclint:none', '-quiet')
}

task jmh(type:JavaExec, dependsOn: 'build') {
	group 'Application'
	description 'Execute benchmarks.'

	main 'org.openjdk.jmh.Main'
	classpath = sourceSets.main.runtimeClasspath
}

tasks.addRule("Pattern: <ClassName>Benchmark : Run benchmark.") { String className ->
	if (className.endsWith("Benchmark")) {
		println "\nRUNNING BENCHMARK: " + className + "\n"

		task "$className"(type: JavaExec, dependsOn: 'build') {
			main 'org.openjdk.jmh.Main'
			args =  ['.' + className + '.*']
			classpath = sourceSets.main.runtimeClasspath
		}
	}
}

// --- wrapper ----------------------------------------------------------------

task wrapper(type: Wrapper) {
	gradleVersion = '3.5'
}
